module System.IO;

import System;

/*
 * This class represents a file as defined by the underlying file system. 
 */
[Bridged(apiset="System.IO.File")]
class File : Item {

	/*
	 * Always return true.
	 *
	 * [return] true
	 */
	public bool isFile(){
		return true;
	}
	
	/*
	 * Create a new file with the specified path.
	 *
	 * [param: path] The path to this file. If this is a relative path, it will be resolved against current working directory.
	 */
	[Bridged(name="ctor")]
	hosted File(string path);

	/*
	 * Get the name of this file. This is only the simple name (with extension) on the path.
	 *
	 * [access] System.IO/stat
	 * [return] The file's name
	 */
	public string getName(){
		return _getName();
	}
	
	[Bridged(name="getName")]
	private hosted string _getName();

	/*
	 * Get the absolute path of this file, including the name.
	 *
	 * [access] System.IO/stat
	 * [return] The file's path, including the name. Note this can be different from the path used 
	 * by the [constructor](#File(string)), which could take a relative path.
	 */	
	public string getPath(){
		return _getPath();
	}
	
	[Bridged(name="getPath")]
	private hosted string _getPath();
	
	/*
	 * Read all the contents from this file as text. This is a rather convenient method for quick scripting, 
	 * but won't scale well if the file is too large and the operation is too frequent. When dealing 
	 * with reading large files at high frequency, always consider using an [asynchronous stream](#getReadStream) first.
	 *
	 * [access] System.IO/read
	 * [return] The contents from this file, in the format of plain ASCII text. 
	 */
	[Bridged(name="readAllText")]
	hosted string readAllText();
	
	/*
	 * Whether this file exist on file system.
	 *
	 * [access] System.IO/stat
	 * [return] True if the file exists.
	 */
	public bool exists(){
		return _exists();
	}
	
	[Bridged(name="exists")]
	private hosted bool _exists();
	
	/*
	 * Create a file represented by this object.
	 *
	 * [access] System.IO/write
	 * [throws: System.IO.IOException] An error occurred during file creation.
	 * [return] True if the file was created; false if the file already exists.
	 */
	public bool create(){
		return _create();
	}
	
	[Bridged(name="create")]
	private hosted bool _create();
	
	/*
	 * Delete this file.
	 *
	 * [access] System.IO/write
	 * [throws: System.IO.IOException] An error occurred during file deletion.
	 * [return] True if the file was successfully deleted; false if the file didn't exist.
	 */
	public bool delete(){
		return _delete();	
	}
	
	[Bridged(name="delete")]
	private hosted bool _delete();
	
	/*
	 * Get the directory this file resides in.
	 *
	 * [access] System.IO/stat
	 * [throws: System.IO.IOException] An error occurred when trying to get the parent directory.
	 * [return] The parent directory. Or null if the path doesn't exist.
	 */	
	public Directory getParent(){
		string pp = _getParentPath();
		if (pp != null) {
			return new Directory(pp);
		} else {
			return null;
		} 
	}
	
	[Bridged(name="getParentPath")]
	private hosted string _getParentPath();
	
	/*
	 * [inherited]
	 * [access] System.IO/write
	 * [param: newName] The new file name. Must not contain FS separator.
	 * [return] True if the file was successfully renamed; false if the file couldn't be.
	 */
	public bool rename(string newName){
		if (newName.contains(Environment.separator)) {
			return false;
		}
		
		return _rename(newName);
	}
	
	[Bridged(name="rename")]
	private hosted bool _rename(string newName);
	
	/*
	 * [inherited]
	 * [access] System.IO/write
	 * [return] True if the file was successfully moved; false if the file couldn't be.
	 */
	public bool move(Directory newDir){
		return _move(newDir);
	}
	
	[Bridged(name="move")]
	private hosted bool _move(Directory newDir);
	
	/*
	 * Get a stream to read from this file. 
	 *
	 * Access policy `System.IO/read` must be enabled to perform read operation on this stream.
	 *
	 * [see: System.IO.FileStream]
	 * [return] A stream that supports reading (both synchronously and asynchronously) but not writing or marking. 
	 */
	public Stream getReadStream(){
		return new FileStream(_getPath(), FileMode.OPEN);
	}
	
	/*
	 * Get a stream to write into this file. 
	 *
	 * Access policy `System.IO/write` must be enabled to perform write operation on this stream.
	 *
	 * [see: System.IO.FileStream]
	 * [return] A stream that supports writing but not reading or marking. 
	 */
	public Stream getWriteStream(bool append){
		return new FileStream(_getPath(), append ? FileMode.APPEND : FileMode.CREATE);
	}
	
}